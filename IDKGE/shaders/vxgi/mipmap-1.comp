#version 460 core
#extension GL_GOOGLE_include_directive: require

#include "../include/UBOs.glsl"
#include "./vxgi.glsl"

layout ( local_size_x = 8, local_size_y = 8, local_size_z = 8 ) in;

layout (binding=20, rgba16f) writeonly uniform image3D un_current_aniso[6];
layout (binding=26, rgba16f) readonly  uniform image3D un_previous_aniso[6];

#define KSIZE 1
#define KSTEP 2
#define KVOLUME 4.0


const ivec3 anisoOffsets[] = ivec3[8]
(
	ivec3(1, 1, 1),
	ivec3(1, 1, 0),
	ivec3(1, 0, 1),
	ivec3(1, 0, 0),
	ivec3(0, 1, 1),
	ivec3(0, 1, 0),
	ivec3(0, 0, 1),
	ivec3(0, 0, 0)
);


void FetchTexels(ivec3 pos, int dir, inout vec4 val[8]) 
{
	for(int i = 0; i < 8; i++)
	{
		val[i] = imageLoad(un_previous_aniso[dir], pos + anisoOffsets[i]);
	}
}


void main()
{
    ivec3 texel  = ivec3(gl_GlobalInvocationID.xyz);


	vec4 values[8];


	// x -

	FetchTexels(2*texel, 0, values);


	// vec4 result = vec4(0.0);

	// for (int i=0; i<4; i++)
	// {
	// 	result += values[i] + values[i+4] * (1.0 - values[i].a);
	// }

	// result /= 4.0;

	imageStore(un_current_aniso[0], texel, 
	(
		values[0] + values[4] * (1 - values[0].a) + 
		values[1] + values[5] * (1 - values[1].a) +
		values[2] + values[6] * (1 - values[2].a) +
		values[3] + values[7] * (1 - values[3].a)) * 0.25f
	);

	// x +
	FetchTexels(2*texel, 1, values);
    imageStore(un_current_aniso[1], texel, 
	(
		values[4] + values[0] * (1 - values[4].a) +
    	values[5] + values[1] * (1 - values[5].a) +
    	values[6] + values[2] * (1 - values[6].a) +
    	values[7] + values[3] * (1 - values[7].a)) * 0.25f
    );
	// y -	
	FetchTexels(2*texel, 2, values);
    imageStore(un_current_aniso[2], texel, 
	(
		values[0] + values[2] * (1 - values[0].a) +
    	values[1] + values[3] * (1 - values[1].a) +
    	values[5] + values[7] * (1 - values[5].a) +
    	values[4] + values[6] * (1 - values[4].a)) * 0.25f
    );
	// y +
	FetchTexels(2*texel, 3, values);
    imageStore(un_current_aniso[3], texel, 
	(
		values[2] + values[0] * (1 - values[2].a) +
    	values[3] + values[1] * (1 - values[3].a) +
    	values[7] + values[5] * (1 - values[7].a) +
    	values[6] + values[4] * (1 - values[6].a)) * 0.25f
    );
	// z -
	FetchTexels(2*texel, 4, values);
    imageStore(un_current_aniso[4], texel, 
	(
		values[0] + values[1] * (1 - values[0].a) +
    	values[2] + values[3] * (1 - values[2].a) +
    	values[4] + values[5] * (1 - values[4].a) +
    	values[6] + values[7] * (1 - values[6].a)) * 0.25f
    );
	// z +
	FetchTexels(2*texel, 5, values);
    imageStore(un_current_aniso[5], texel, 
	(
		values[1] + values[0] * (1 - values[1].a) +
    	values[3] + values[2] * (1 - values[3].a) +
    	values[5] + values[4] * (1 - values[5].a) +
    	values[7] + values[6] * (1 - values[7].a)) * 0.25f
    );
}

